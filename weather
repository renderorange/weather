#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long  ();
use Pod::Usage    ();
use File::HomeDir ();
use Config::Tiny  ();
use HTTP::Tiny    ();
use JSON::MaybeXS ();

my $VERSION = '2.00';

my $opt = {};

Getopt::Long::GetOptions(
    "current"  => \$opt->{current},
    "forecast" => \$opt->{forecast},
    "version"  => \$opt->{version},
    "help"     => \$opt->{help},
    "man"      => \$opt->{man},
    "devel"    => \$opt->{devel},
) or Pod::Usage::pod2usage( -exitval => 1, -verbose => 0 );

Pod::Usage::pod2usage( -exitval => 0, -verbose => 1 ) if $opt->{help};
Pod::Usage::pod2usage( -exitval => 0, -verbose => 2, noperldoc => 1 ) if $opt->{man};

if ( $opt->{version} ) {
    print "$VERSION\n";
    exit 0;
}

Pod::Usage::pod2usage( -exitval => 1, -verbose => 0 ) unless ( $opt->{current} || $opt->{forecast} );

my $home      = File::HomeDir->my_home;
my $file      = '.weatherrc';
my $file_path = "$home/$file";

die( "$file_path does not exist\n" ) unless -e $file_path;
my $config = Config::Tiny->read( $file_path );

my $tier = $opt->{devel} ? 'development' : 'production';
die( "the $tier key for openweathermap is not present in $file_path\n" )
    unless exists $config->{openweathermap}->{$tier}
        && $config->{openweathermap}->{$tier} =~ /^\w+$/;

my $http_tiny = HTTP::Tiny->new();

my $geolocation_api = 'http://ip-api.com/json';
my $geo_res = $http_tiny->get( $geolocation_api );

my $location = {};

if ( $geo_res->{success} ) {
    my $content = JSON::MaybeXS::decode_json( $geo_res->{content} );

    # TODO: this should be a little cleaner
    # to request only the fields we want
    $location->{zip}      = $content->{zip};
    $location->{city}     = $content->{city};
    $location->{state}    = $content->{regionName};
    $location->{country}  = $content->{country};
    $location->{timezone} = $content->{timezone};
    $location->{lon}      = $content->{lon};
    $location->{lat}      = $content->{lat};
}
else {
    die( "the query to $geolocation_api wasn't successful\n" );
}

my $current_conditions_api = "https://api.openweathermap.org/data/2.5/weather?zip=$location->{zip},$location->{country}&appid=$config->{openweathermap}->{$tier}";
my $current_res = $http_tiny->get( $current_conditions_api );

__END__

=pod

=head1 NAME

weather - cli program to get the weather for your location

=head1 SYNOPSIS

  weather [--current] [--forecast]
          [--version]
          [--help] [--man]
          [--devel]

=head1 DESCRIPTION

This program will retrieve weather condition and forecast details for your location.

=head1 OPTIONS

  --current     print the current conditions
  --forecast    print the next 5 day forecast
  --version     print the version and exit
  --help        print this dialogue
  --man         display the full documentation
  --devel       use a development tier key for openweathermap

=head1 EXAMPLES

=over

=item get the current conditions

  weather --current

=item get the current conditions and 5 day forecast

  weather --current --forecast

=item get the 4 day forecast only

  weather --forecast

=back

=head1 CONFIGURATION

The configuration file for weather is located in the homedir of the running user, named .weatherrc

The file may contain two sections, production and development, under the parent section, openweathermap

  ~ $ cat .weatherrc
  [openweathermap]
  production = 1q2w3e4r5t6y7u8i9o0p1q2w3e4r5t6y
  development = 0p9o8i7u6y5t4r3e2w1q0p9o8i7u6y5t

The development section is required in the case of using the --devel switch for weather.

The production section is required in normal operation.

=head1 DEPENDENCIES

=over

=item Getopt::Long

=item Pod::Usage

=item File::HomeDir

=item Config::Tiny

=item HTTP::Tiny

=item JSON::MaybeXS

=back

=head1 AUTHOR

Blaine Motsinger, <blaine@renderorange.com>

=cut
